#version 430

layout(binding = 1) uniform sampler3D TexInputVolume;
layout(binding = 2) uniform sampler1D TexInputTransferFunc;  

uniform vec3 ExtCoefVolumeResolution;
uniform vec3 ExtCoefVoxelSize;

uniform float S0;
uniform vec3 VolumeGridSize;

// size of each work group
layout (local_size_x = 8, local_size_y = 8, local_size_z = 8) in;
layout (r16f, binding = 0) uniform image3D TexBaseLevelExtCoefVolume;

float GetFromInputData (vec3 tx_pos)
{
  // First, we normalized the current tex coordinate...
  tx_pos = tx_pos / VolumeGridSize;

  // If we're outside the volume, just return 0
  if (tx_pos.x < 0.0f || tx_pos.y < 0.0f || tx_pos.z < 0.0f 
   || tx_pos.x > 1.0f || tx_pos.y > 1.0f || tx_pos.z > 1.0f)
    return 0.0f;
  
  // Get the current opacity from the input volume
  float op = texture(TexInputTransferFunc, texture(TexInputVolume, tx_pos).r).a;

  // Test: alpha correction (decreases the amount of obstruction from farthest occluders)
  //op = (1.0f - pow(1.0f - op, S0));

  // return the opacity
  return op;
}

void main ()
{
  ivec3 storePos = ivec3(gl_GlobalInvocationID.xyz);
  
  float sum_wkck = 0.0f;
  float sum_wk   = 0.0f;
  
  // if storePos is out of the current volume being computed
  if (storePos.x > ExtCoefVolumeResolution.x - 1
   || storePos.y > ExtCoefVolumeResolution.y - 1
   || storePos.z > ExtCoefVolumeResolution.z - 1)
    return;
  
  // First, compute the grid position based on the new volume's voxel size
  vec3 grid_pos = (vec3(storePos) + 0.5f) * ExtCoefVoxelSize;

  // Compute the current Gaussian filter from [-vtk, vtk]
  int vtk = 3;
  for (int ptx = -vtk; ptx < vtk + 1; ptx++)
  {
    for (int pty = -vtk; pty < vtk + 1; pty++)
    {
      for (int ptz = -vtk; ptz < vtk + 1; ptz++)
      {
        // https://en.wikipedia.org/wiki/Gaussian_filter
        // Get interpolated samples maintaining the defined standard deviation
        float fx = float(ptx) * S0;
        float fy = float(pty) * S0;
        float fz = float(ptz) * S0;
        
        float wk = (S0*S0*S0) * exp(-(fx*fx + fy*fy + fz*fz) / (2.0f*S0*S0));

        float ck = GetFromInputData(grid_pos + vec3(fx, fy, fz));
       
        sum_wkck += wk * ck;
        sum_wk += wk;
      }
    }
  }

  imageStore(TexBaseLevelExtCoefVolume, storePos, vec4(sum_wkck / sum_wk, 0.0f, 0.0f, 0.0f));
}